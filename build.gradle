buildscript {
    repositories {
        jcenter()
        mavenLocal()
    }
    dependencies {
        classpath 'com.novoda:bintray-release:0.4.0'
        classpath 'org.jetbrains.dokka:dokka-android-gradle-plugin:0.9.14'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.novoda.bintray-release'
apply plugin: 'org.jetbrains.dokka-android'

ext {
    libVersion = "1.0.0-RC1"
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName libVersion
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'io.reactivex.rxjava2:rxjava:2.0.8'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

publish {
    bintrayUser = 'vincentmasselis'
    groupId = 'com.vincentmasselis'
    artifactId = 'rx-ui-kotlin'
    publishVersion = libVersion
    autoPublish = false
}

dokka {
    moduleName = 'RxUIKotlin'
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/libs"
    processConfigurations = ['compile']
    includes = ['README.md']
    //samples = ['samples/basic.kt', 'samples/advanced.kt']
    linkMapping {
        dir = "src/main/kotlin"
        url = "https://github.com/VincentMasselis/RxUIKotlin/tree/master/src/main/kotlin"
        suffix = "#L"
    }
    sourceDirs = files('src/main/kotlin')
}

// Command for a new release :
// ./gradlew :rxinterfacekotlin:clean :rxinterfacekotlin:build :rxinterfacekotlin:generatePomFileForReleasePublication :rxinterfacekotlin:sourcesJar :rxinterfacekotlin:dokka :rxinterfacekotlin:bintrayUpload -PbintrayKey=MY_API_KEY